# Maintainer: Christophe Gueret <christophe.gueret@gmail.com>

pkgname=sugar-toolkit-gtk3
pkgver=0.96.5
pkgrel=3
pkgdesc="Set of widgets for Sugar applications"
arch=('i686' 'x86_64')
url="http://www.sugarlabs.org/"
license=('GPL')
groups=('sugar-desktop' 'glucose')
depends=('python2' 'sugar-datastore' 'sugar-presence-service' 'hippo-canvas' 'alsa-lib' 'python2-gconf' 'python2-rsvg')
makedepends=('intltool>=0.33' 'gcc' 'perl>=5.8.1')
source=(http://download.sugarlabs.org/sources/sucrose/glucose/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('d6328db8ef711e522fb13f45f584ea81')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=python2 ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # python2 fix (taken from pyrss PKGBUILD, modified a bit)
  for file in $(find ${pkgdir}/usr/bin -name 'sugar*'); do
        sed -i 's_^python_python2_' $file
        sed -i 's_^#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done
}


