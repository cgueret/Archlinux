# Maintainer: Christophe Gueret <christophe.gueret@gmail.com>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgname=sugar-datastore
pkgver=0.96.0
pkgrel=2
pkgdesc="Backend of the Sugar Journal"
arch=('i686' 'x86_64')
url="http://www.sugarlabs.org/"
license=('GPL')
groups=('sugar-desktop' 'glucose')
depends=('python2' 'python2-dbus' 'python2-gobject' 'sugar-base' 'python2-xapian' 'python2-cjson')
makedepends=('gcc')
source=(http://download.sugarlabs.org/sources/sucrose/glucose/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('55112bc2712ca1de7b7968360eb803c7')


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=python2 ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # python2 fix
  for file in copy-from-journal copy-to-journal datastore-service; do
        sed -i 's_^python_python2_' ${pkgdir}/usr/bin/${file}
        sed -i 's_^#!/usr/bin/env python_#!/usr/bin/env python2_' ${pkgdir}/usr/bin/${file}
  done
}


