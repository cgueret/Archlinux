# Maintainer: Christophe Gueret <christophe.gueret@gmail.com>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgname=sugar
pkgver=0.96.3
pkgrel=3
pkgdesc="Sugar is the core component of a worldwide effort to provide every child with equal opportunity for a quality education"
arch=('i686' 'x86_64')
url="http://www.sugarlabs.org/"
license=('GPL')
groups=('sugar-desktop' 'glucose')
depends=('python2' 'gtk2' 'telepathy-mission-control' 'python-simplejson' 'python2-wnck' 'sugar-datastore' 'sugar-artwork' 'sugar-toolkit' 'metacity' 'python2-dateutil' 'sugar-toolkit-gtk3')
makedepends=('gcc' 'perl>=5.8.1')
optdepends=('xorg-server-xephyr: Needed for sugar-emulator')
source=(http://download.sugarlabs.org/sources/sucrose/glucose/${pkgname}/${pkgname}-${pkgver}.tar.bz2 fix_shell_posix_and_quoting.patch fix_missing_default_layout.patch)    
md5sums=('e4fa6c57b3289956bdc2e91e0211d08b'
         'f3d69de5ccab6fe1ff645b6f93d6322c'
         '5abd60b0692767411155d358500c7222')



build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Patch from Debian http://patch-tracker.debian.org/patch/series/view/sugar-0.90/0.90.3-1/fix_shell_posix_and_quoting.patch
  # manually edited to fix a missing "]" bug
  patch -p1 < ../../fix_shell_posix_and_quoting.patch

  # Fix a layout returned to be "None" sometimes
  patch -p1 < ../../fix_missing_default_layout.patch

  # Fix from http://launchpadlibrarian.net/53053401/DCON.patch	
  # patch -p1 < ../../DCON.patch

  PYTHON=python2 ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # python2 fix (taken from pyrss PKGBUILD, modified a bit)
  for file in $(find ${pkgdir}/usr/bin -name 'sugar*'); do
        sed -i 's_^python_python2_' $file
        sed -i 's_^#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done
}



